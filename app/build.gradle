apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26

    def versionProp = file('version.properties')
    if (versionProp.canRead()) {

        // Get properties from version.properties
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionProp))

        // Increase versionCode
        def code = versionProps['VERSION_CODE'].toInteger() + 1

        // Write new versionCode to version.properties
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionProp.newWriter(), null)

        defaultConfig {
            applicationId "com.teamdarkness.godlytorch"
            minSdkVersion 21
            targetSdkVersion 26
            versionCode code
            versionName "1.1.0"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file("../debug-key")
            storePassword 'godlytorch'
            keyAlias 'godly'
            keyPassword 'godlytorch'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':croller')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.5.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation 'com.google.firebase:firebase-core:11.6.0'
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation 'eu.chainfire:libsuperuser:1.0.0.+'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
}

apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'